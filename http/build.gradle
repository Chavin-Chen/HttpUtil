plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly 'org.json:json:20220924'
}

publishing {
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project['ossrh.username']
                password = project['ossrh.password']
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.chavin-chen'
            artifactId = 'util-http'
            version = '1.0.0'
            from components.java
            pom {
                name = 'Http Util'
                description = 'This is a light net lib based on HttpUrlConnection'
                url = 'https://github.com/Chavin-Chen/HttpUtil'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Chavin Chen'
                        email = 'chavinchen@hotmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/Chavin-Chen/HttpUtil/tree/master'
                    connection = 'scm:git:git://github.com/Chavin-Chen/HttpUtil.git'
                    developerConnection = 'scm:git:ssh://github.com:Chavin-Chen/HttpUtil.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}